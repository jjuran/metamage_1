#!/usr/bin/env minivx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let fifo-icons = import "fifo-icons"

assert argv[ 1 ] == "-o"

let output_dir = argv[ 2 ] "/"

let levels = fifo-icons.guage_levels

let manifest =
[
	131: fifo-icons.basic_icon,
	132: fifo-icons.half_open_reading,
	133: fifo-icons.half_open_writing,
	134: fifo-icons.pipe_underrun,
	135: fifo-icons.empty_guage,
	
	*(0 -> 16 map { 136 + v => levels[ v ] }),
	
	152: fifo-icons.full_guage,
	153: fifo-icons.pipe_overrun,
	154: fifo-icons.writer_closed,
	155: fifo-icons.broken_pipe,
]

for icon in manifest do
{
	let base = "00" hex icon.key
	let stem = output_dir base
	let data = icon.value
	
	let get = fifo-icons.get % data
	
	for icon-type in ["ICN# icl4 icl8" / ' '] do
	{
		rewrite stem "." icon-type <== get icon-type
	}
}
