#!/usr/bin/vx -Z

const host = "www.macrelix.org"

const relix_url = "http://%s/pub/channel/46e70108/" % host

const key = "46e70108538e13868fc8d8454610d47e9ca09d934aa2224af806547190aa7a93"

const platform =
	if argc > 1
		then {argv[ 1 ]}
		else {(load "/etc/platform").lines()[ 0 ]}

warn "Checking " host " for latest version..."

try
{
	system "htget http://" host " > /dev/null 2>&1"
}
catch
{
	warn "upgrade: can't reach " host
	exit 1
}

const prefix = "relix-" platform "_"

const list = output-from-run [ "htget", relix_url ]

var last = ()

for line in list.lines() do
{
	if var p = (begin line).find prefix then
	{
		const q = p + prefix.length + "20210307-1958".length  # minus the ".gz"
		
		last = q - p
	}
}

def exists (path)
{
	return try {stat path; true} catch {false}
}

const dest = "/sys/app/dir/../" last

if exists dest then
{
	warn "You already have " last "."
	exit 0
}

chdir "/tmp"

warn "Downloading " last ".gz..."

const file = last ".gz"
const mbim = last ".mbim"

system "htget --dump-progress -O " relix_url file " | progress Downloading"

warn "Checking signature..."

run ["arcsign unseal -K" / ' ', key, file]

warn "Decompressing..."

run ["gzip", "-d", file]

rename (last, mbim)

warn "Decoding archive..."

run ["macbin", "decode", mbim]

rename (last, dest)
remove mbim

run ["sync"]

warn "Done."
