#!/usr/bin/vx -Z

let host = "www.macrelix.org"

let relix_url = "http://%s/pub/channel/46e70108/" % host

let key = "46e70108538e13868fc8d8454610d47e9ca09d934aa2224af806547190aa7a93"

let platform =
	if argc > 1
		then {argv[ 1 ]}
		else {(load "/etc/platform").lines()[ 0 ]}

warn "Checking " host " for latest version..."

try
{
	output-from-run [ "htget -I http://" host / ' ' ]
}
catch
{
	warn "upgrade: can't reach " host
	exit 1
}

let prefix = "relix-" platform "_"

let list = output-from-run [ "htget", relix_url ]

var last = ()

for line in list.lines() do
{
	if var p = (begin line).find prefix then
	{
		let q = p + prefix.length + "20210307-1958".length  # minus the ".gz"
		
		last = q - p
	}
}

def exists (path)
{
	return try {stat path; true} catch {false}
}

let dest = "/sys/app/dir/../" last

if exists dest then
{
	warn "You already have " last "."
	exit 0
}

chdir "/tmp"

warn "Downloading " last ".gz..."

let file = last ".gz"
let mbim = last ".mbim"

let download = "graft --fd 1 progress Downloading // "
               "htget --dump-progress -O" / ' ', relix_url file

run [ download ]

warn "Checking signature..."

run ["arcsign unseal -K" / ' ', key, file]

warn "Decompressing..."

run ["gzip", "-d", file]

rename (last, mbim)

warn "Decoding archive..."

run ["macbin", "decode", mbim]

rename (last, dest)
remove mbim

sync()

warn "Done."
