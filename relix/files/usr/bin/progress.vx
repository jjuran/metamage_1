#!/usr/bin/vx -Z

let title = if argc > 1 then {argv[ 1 ]}

chdir "/gui/new/port"

let LF = "\n"

if title then
{
	rewrite "title" <== title LF
}

rewrite "procid" <== 5 LF
rewrite "size"   <== "280,76" LF

link( "/gui/new/stack", "view" )

link( "/gui/new/frame", "v/meter/view" )

let meter = "v/meter/v/"

rewrite meter "height" <== 11 LF

rewrite meter ".margin-left"   <== 10 LF
rewrite meter ".margin-right"  <== 10 LF
rewrite meter ".margin-bottom" <== 10 LF

link( "/gui/new/progress", meter "view" )

touch "window"

def line_buffered (input)
{
	var buffer = ""
	
	return lambda
	{
		while true do
		{
			while var p = (begin buffer).find '\n' do
			{
				# Strip newline before returning, and don't return blanks
				
				let result = p.past
				
				buffer = (++p).rest
				
				if result then
				{
					return result
				}
			}
			
			buffer .= (input.read 64 or return ())  # 64 is overkill, really
		}
	}
}

for line in receiver line_buffered IN do
{
	var value = line
	
	if '%' in value then
	{
		value = int [value / '%'][0] * 100
	}
	else if '/' in value then 
	{
		let (numer, denom) = *([value / '/'] map int)
		
		value = numer * 10000 div denom
	}
	
	rewrite meter "v/value" <== value LF
}

end.
