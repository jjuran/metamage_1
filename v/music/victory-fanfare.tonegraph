# Final Fantasy victory fanfare

# 1.059463094359295^12 ~= 2
# 1.059463094359295 * 2^16 ~= 69432.9733519308 ~= 69433

let semitone_numer = 69433
let semitone_denom = 2^16

def transpose (n, pitch) { pitch * semitone_numer^n div semitone_denom^n }

let C8 = 0x184ada * 2;
let C0 = C8 div 2^8

let adj = 3

def C  (o) { transpose( adj, C8 div 2^(8 - o) ) }
def D  (o) { transpose( 2, C o ) }
def Eb (o) { transpose( 3, C o ) }
def E  (o) { transpose( 4, C o ) }
def F  (o) { transpose( 5, C o ) }
def G  (o) { transpose( 7, C o ) }
def Ab (o) { transpose( 8, C o ) }
def Bb (o) { transpose( 10, C o ) }

let audiograph =
[
	# By Nobuo Uematsu.
	# 8-bit arrangement and notation copyright 2021 Joshua Juran.
	
	1:  [ C  2 ],
	1:  [ E  2 ],
	1:  [ G  2 ],
	1:  [ C  3 ],
	1:  [ E  3 ],
	1:  [ G  3 ],
	1:  [ C  4 ],
	1:  [ E  4 ],
	1:  [ G  4 ],
	
	1:  [ C  4, G 3, E  4, C  5 ],
	1:  [ C  4 ],
	1:  [ G  3, G 3, E  4, C  5 ],
	1:  [ G  3 ],
	1:  [ E  3, G 3, E  4, C  5 ],
	1:  [ E  3 ],
	6:  [ C  3, G 3, E  4, C  5 ],
	
	6:  [ Eb 3, C 4, Eb 4, Ab 4 ],
	6:  [ F  3, D 4, F  4, Bb 4 ],
	
	3:  [ C  3, E 4, G  4, C  5 ],
	1:  [],
	2:  [ C  3, D 4, F  4, Bb 4 ],
	12: [ C  3, E 4, G  4, C  5 ],
	6:  [],
]
map { v.key * 3 / 5 => v.value }
