module byte-order

def same-endian-fields (T)
{
	const fields = type: T,
	               encode: T.encode,
	               decode: T.decode,
	               size: T.size
	
	return fields
}

def cross-endian-fields (T)
{
	const fields = type: T,
	               encode: { -(T.encode _) },
	               decode: { T.decode (-_) },
	               size: T.size
	
	return fields
}

const same-cross = [ same-endian-fields, cross-endian-fields ]

const big-little = u16.encode 1 map { same-cross[ int v ] }

const big-endian-fields,
const little-endian-fields = *big-little

export
def native (T)
{
	const fields = order: native,
	               same-endian-fields T
	
	return record fields
}

export
def big-endian (T)
{
	const fields = order: big-endian,
	               big-endian-fields T
	
	return record fields
}

export
def little-endian (T)
{
	const fields = order: little-endian,
	               little-endian-fields T
	
	return record fields
}
