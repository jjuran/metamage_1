#!/usr/bin/env lvx

if argc < 2 then
{
	warn "usage: sndplay tonegraph | sndtrack"
	exit 1
}

let tonegraph = eval (load, realpath) argv[ 1 ]

def connect_output()
{
	if let sndpipe_fd = getenv "SNDPIPE_FD" then
	{
		return fd int sndpipe_fd
	}
	
	if isatty OUT then
	{
		warn "sndplay: Pipe output to sndtrack, please"
		exit 1
	}
	
	return OUT
}

let output_fd = connect_output()

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/v/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let sndpipe = import "sndpipe"

let sound = sndpipe.player ({ output_fd <== _ }, output_fd.read)

def normalize (wave)
{
	let w = wave.key
	let i = wave.value
	
	return w[ i -> 256 ] w[ 0 -> i ]
}

def blend (waves)
{
	let weight = waves map .key per Math.sum
	
	let samples = 0 -> 256 map
	{
		let i = v
		
		byte ((waves map { v.key * (u8 v.value[ i ] - 128) }
		             per Math.sum)
		      div weight + 128),
	}
	
	return packed samples
}

def harmonics (wave, weights)
{
	let n = weights.length
	
	return 0 -> n map
	{
		let i = v + 1
		let x = weights[ v ]
		
		x and x => mince (wave * i, i),
	}
}

let triangle = do
{
	let tooth = 1 -> 128 map {byte (2 * v)} per packed
	
	packed (tooth x"FF" -tooth x"01") => 64,
}

let env_vox  = do { (let file = getenv "SNDPLAY_VOX" ) and packed load file }
let env_wave = do { (let file = getenv "SNDPLAY_WAVE") and packed load file }

if env_vox and env_wave then
{
	warn "note: SNDPLAY_VOX takes precedence over SNDPLAY_WAVE"
}

let root = env_wave or normalize triangle

let weights = [5, 2, 0, 1]

let vox = env_vox or blend harmonics (env_wave or normalize triangle, weights)

def play (score, wave=(vox => 0))
{
	sound.four_tone (32767, [ (0: wave) (*) 4 ])
	
	for chord in score do
	{
		let duration = chord.key / 8
		let notes    = chord.value map { v => wave }
		
		sound.tone_update (32767, notes)
		
		sleep duration
	}
	
	sound.stop()
}

play tonegraph

end.
