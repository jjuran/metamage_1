#!/usr/bin/env vx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/v/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let date-time = import "date-time"

let days   = date-time.days
let months = date-time.months
let gmtime = date-time.gmtime

def left-pad (fill, width, x)
{
	let s = str x
	
	return (fill * (width - s.length)) s
}

let tm = gmtime time()

let hms = [tm.hour, tm.min, tm.sec] map left-pad % "0" % 2 per {a ":" b}

let parts = days[ tm.wday ][ 0 -> 3 ],
            months[ tm.mon ][ 0 -> 3 ],
            left-pad( " ", 2, tm.mday ),
            hms,
            tm.zone,
            tm.year

print " ".join parts
