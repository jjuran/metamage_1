#!/usr/bin/env lvx

def connect_output()
{
	if let sndpipe_fd = getenv "SNDPIPE_FD" then
	{
		return fd int sndpipe_fd
	}
	
	if isatty OUT then
	{
		warn "sndtune: Pipe output to sndtrack, please"
		exit 1
	}
	
	return OUT
}

let output_fd = connect_output()

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/v/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let sndpipe = import "sndpipe"

let sound = sndpipe.player ({ output_fd <== _ }, output_fd.read)

let square   = packed (x"01" * 128, x"ff" * 128) => 0
let sawtooth = 0 -> 256 map byte per packed => 128

let triangle = do
{
	let tooth = 1 -> 128 map {byte (2 * v)} per packed
	
	packed (tooth x"FF" -tooth x"01") => 64,
}

# 256/370 ~= 45344

# 1.059463094359295^12 ~= 2
# 1.059463094359295 * 2^16 ~= 69432.9733519308 ~= 69433

let semitone_numer = 69433
let semitone_denom = 2^16

def transpose (n, pitch)
{
	return pitch * semitone_numer^n div semitone_denom^n
}

let C8 = 0x1812eb * 2;

let adj = 3

def C (o) { transpose( adj, C8 div 2^(8 - o) ) }
def D (o) { transpose( 2, C o ) }
def E (o) { transpose( 4, C o ) }
def F (o) { transpose( 5, C o ) }
def G (o) { transpose( 7, C o ) }
def A (o) { transpose( 9, C o ) }
def B (o) { transpose( 11, C o ) }

let score =
[
	# Melody by Lonely Island.
	# 8-bit arrangement and notation copyright 2020 Joshua Juran.
	
	6: [ A 3, C 4, E 4 ],
	8: [ A 3, C 4, F 4 ],
	2: [ A 3, C 4, C 4 ],
	6: [ G 3, C 4, E 4 ],
	2: [ G 3, B 3, E 4 ],
	6: [ G 3, B 3, D 4 ],
	2: [ G 3, G 3, D 4 ],
	
	6: [ A 3, C 4, E 4 ],
	8: [ A 3, C 4, F 4 ],
	2: [ A 3, C 4, C 4 ],
	2: [ G 3, C 4, E 4 ],
	2: [ G 3, C 4, F 4 ],
	2: [ E 3, C 4, G 4 ],
	8: [ D 3, B 3, G 4 ],
]

def play (score, wave=triangle)
{
	sound.four_tone (255, [ (0: wave) (*) 4 ])
	
	for chord in score do
	{
		let duration = chord.key / 7
		let notes    = chord.value map { v => wave }
		
		sound.tone_update (255, notes)
		
		sleep duration
	}
	
	sound.stop()
}

play score
