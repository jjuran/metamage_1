#!/usr/bin/env vx
#
# Copyright 2017 Josh Juran.  All rights reserved.
#
# "chutesay" is a trademark of Josh Juran.
# The stick-figure parachute messenger is a trademark of Josh Juran.

def calc-width (lines: str[])
{
	# The more lines we have, the wider we have to be.
	
	return lines per 0 >-{ Math.max( L + 2, v.length ) }
}

def odd-padded (x: int)
{
	return x + 1 - x mod 2
}

if argc < 2 then
{
	warn "chutesay: argument required"
	exit 2
}

let text-lines = [ "", *argv[ 1 -> argc ] ]

let n = text-lines.length

let widest    = 6 + odd-padded calc-width text-lines
let narrowest = widest - n * 2

def spc (n: int) { " " * n }

let canopy-top = spc(n + 1) ("_" * narrowest)

var i = n
var width = narrowest

def centered (text: str)
{
	let margin = width - text.length
	
	width += 2
	
	let pre  = margin div 2
	let post = margin - pre
	
	return (spc pre) text (spc post)
}

let canopy-mid = text-lines map { (spc i--) "/" (centered v) "\\" }

let canopy-bottom = "(" ("_" * widest) ")"

let canopy = canopy-top,
             *canopy-mid,
             canopy-bottom

let halflen = widest div 2

# shroud lines
let lines = 1 -> halflen map { (spc v) "\\" spc( widest - 2 * v ) "/" }

let indent = spc halflen

let pilot =
[
	"\\o/",  # \o/
	"/H\\",  # /H\
	"/ \\",  # / \
]
map { indent v }

print join( "\n", canopy, *lines, *pilot, "" )

end.

    _________________
   /                 \
  /  That escalated   \
 /       quickly       \
(_______________________)
 \                     /
  \                   /
   \                 /
    \               /
     \             /
      \           /
       \         /
        \       /
         \     /
          \   /
           \o/
           /H\
           / \
