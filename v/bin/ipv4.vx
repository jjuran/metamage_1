#!/usr/bin/env vx -Z

def exists (path)
{
	return try {stat path; true} catch {false}
}

var ifconfig-path = "/sbin/ifconfig"

if not exists ifconfig-path then
{
	ifconfig-path = "ifconfig"  # for Android
}

const lower = 'a' .. 'z'
const digit = '0' .. '9'
const space = ' ' | '\t'

const name = (lower | digit | '-' | '_')+ => str
const inet = [space*, "inet ", "addr:"?]
const addr = (digit | '.')+ => str

var results = []

var interface

for line in output-from-run( [ifconfig-path] ).lines() do
{
	if line == "" then {continue}
	
	var p = begin line
	
	if var intf_name = p += name then
	{
		interface = intf_name
	}
	else if p += inet then
	{
		const ipv4 = p += addr
		
		results <-- interface ": " ipv4
	}
}

print "\n".join *(Iter.sorted results)
