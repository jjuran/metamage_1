#!/usr/bin/env vx -Z

def exists (path)
{
	return try {stat path; true} catch {false}
}

var ifconfig-path = "/sbin/ifconfig"

if not exists ifconfig-path then
{
	ifconfig-path = "ifconfig"  # for Android
}

let lower = 'a' .. 'z'
let digit = '0' .. '9'
let space = ' ' | '\t'

let name = (lower | digit | '-' | '_')+ => str
let inet = [space*, "inet ", "addr:"?]
let addr = (digit | '.')+ => str

var results = []

var interface

for line in output-from-run( [ifconfig-path] ).lines() do
{
	if line == "" then {continue}
	
	var p = begin line
	
	if var intf_name = p += name then
	{
		interface = intf_name
	}
	else if p += inet then
	{
		let ipv4 = p += addr
		
		results <-- interface ": " ipv4
	}
}

print "\n".join *(Iter.sorted results)
