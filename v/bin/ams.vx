#!/usr/bin/env minivx

let bindir = dirname realpath argv[ 0 ]

def existing (path)
{
	return try {stat path; path} catch {}
}

let exists = existing

let amsdir = getenv "AMS_ROOT" or existing bindir "/../share/ams/"
                               or existing bindir "/../../../ams-68k-bin"
                               or throw "AMS_ROOT not set; can't find AMS files"

let ams_bin = getenv "AMS_BIN" or amsdir "/bin"
let ams_lib = getenv "AMS_LIB" or amsdir "/lib"
let ams_mnt = getenv "AMS_MNT" or amsdir "/mnt"

let title = "Advanced Mac Substitute"

var disks = []

var live_modal = false

var cpu = null
var mem = null
var modem = null
var app_name = null
var screen_res = null
var has_cursor = bool getenv "AMS_CURSOR"

var args = begin argv

++args

var arg = null

while args do
{
	arg = *args++
	
	if substr (arg, 0, 2) == "--" then
	{
		if arg == "--" then
		{
			arg = if args then { *args++ }
			
			break
		}
		else if arg == "--cpu" then
		{
			cpu = *args++
		}
		else if arg == "--disk" then
		{
			disks <-- *args++
		}
		else if arg in ["--hardware-cursor", "--hwcursor"] then
		{
			has_cursor = true
		}
		else if arg == "--live-modal" then
		{
			live_modal = true
		}
		else if arg == "--mem" then
		{
			mem = *args++
		}
		else if arg == "--modem" then
		{
			modem = *args++
		}
		else if arg == "--res" then
		{
			screen_res = *args++
		}
		else
		{
			warn "ams.vx: Unrecognized option: " arg
			
			exit 50
		}
	}
	else
	{
		break
	}
}

if arg then
{
	app_name = arg
}

if args then
{
	warn "ams.vx: Ignoring extra arguments"
}

let model = cpu or getenv "AMS_CPU"         # may be ()
let model_opt = if model then {"-M" model}  # may be ()

mem = mem or getenv "AMS_MEM"

let mem_opt = if mem then
{
	# 128K, 512K, 1 MiB, 2 MiB, 2.5 MiB, 4 MiB
	assert int mem in [ 0, 1, 2, 4, 5, 8 ]
	
	"--mem=" mem,
}

screen_res = screen_res or getenv "AMS_SCREENRES" or "512x342"

let default_app = "Welcome"

if app_name isa null then
{
	app_name =
	if let ams_appname = getenv "AMS_APPNAME"
		then {ams_appname}
	else {default_app}
}

def which (program)
{
	return
	try
	{
		output-from-run [ "which", program ],
	}
	catch {}
}

let home     = getenv "HOME"

let root_dir = existing home "/68k" or ams_mnt

let resfiles = [ "AMS Resources", "System" ]
let respaths = resfiles map { root_dir "/" v } ver { exists v "/r" }

if exists bindir "/mkrsrc.vx" then
{
	let output = "-o", root_dir "/AMS Resources/rsrc"
	
	run [ "minivx", bindir "/mkrsrc.vx", output, *respaths ]
}

def freemountd_rw (root) { "freemountd -qu --rw --root" / ' ', root, "//" }

let freemountd-server = "freemountd -qu --root" / ' ', root_dir, "//"

let xv68k = "xv68k"

def module_path (name)
{
	return ams_lib "/" name
}

# Reserve fds 6 and 7
dup2(ERR, 6)
dup2(ERR, 7)

let events_fd_slot = reader "/dev/null"
let events_fd_opt  = "--events-fd=" events_fd_slot

var sound-fd
var sound-server = ()

def get_sound_fd
{
	if let sound_fd = getenv "AMS_SOUND_FD" then
	{
		return sound_fd
	}
	
	if which "sndtrack" then
	{
		let sound_fd_slot = str (sound-fd = reader "/dev/null")
		sound-server = "--fd", sound_fd_slot, "sndtrack", "//"
		
		return sound_fd_slot
	}
	
	return ()
}

var modem-fd
var modem-server = ()

def get_modem_fd
{
	if modem = modem or getenv "AMS_MODEM" then
	{
		let (host, port) = modem / ':'
		
		let command = "tcpclient", host, port, "duplex", "6-1", "0-7"
		
		let modem_fd_slot = str (modem-fd = reader "/dev/null")
		modem-server = "--fd", modem_fd_slot, command, "//"
		
		return modem_fd_slot
	}
	
	return ()
}

var appfs-fd
var appfs-server = ()

def get_appfs_fd
{
	var appfs = getenv "AMS_APPFS" or return ()
	
	if appfs == "-" then
	{
		let osx_prefs = home "/Library/Preferences"
		let ams_prefs = if exists osx_prefs
		then { osx_prefs "/Advanced Mac Substitute" }
		else { home "/.advanced-mac-substitute" }
		
		appfs = ams_prefs "/" app_name
		
		if not exists appfs then
		{
			run [ "mkdir -p" / ' ', appfs ]
		}
	}
	
	let fd_slot = str (appfs-fd = reader "/dev/null")
	appfs-server = "--fd", fd_slot, freemountd_rw appfs
	
	return fd_slot
}

var docfs-fd
var docfs-server = ()

def get_docfs_fd
{
	var docfs = getenv "AMS_DOCFS" or return ()
	
	if docfs == "-" then
	{
		let osx_docs = home "/Documents"
		let ams_docs = if exists osx_docs
		then { osx_docs "/Advanced Mac Substitute" }
		else { home "/.advanced-mac-substitute/documents" }
		
		docfs = ams_docs "/" app_name
		
		if not exists docfs then
		{
			run [ "mkdir -p" / ' ', docfs ]
		}
	}
	
	let fd_slot = str (docfs-fd = reader "/dev/null")
	docfs-server = "--fd", fd_slot, freemountd_rw docfs
	
	return fd_slot
}

def core_opts
{
	var opts = [ events_fd_opt ]
	
	if let auto_clear = getenv "AMS_AUTOCLEAR" then
	{
		opts <-- "-0"
	}
	
	if let auto_vsync = getenv "AMS_AUTOVSYNC" then
	{
		opts <-- "-Y"
	}
	
	if let gne_ticks = getenv "AMS_GNETICKS" then
	{
		opts <-- "-G" gne_ticks
	}
	
	if let romgen = getenv "AMS_ROMGEN" then
	{
		opts <-- "-R" romgen
	}
	
	if let system = getenv "AMS_SYSTEM" then
	{
		opts <-- "-V" system
	}
	
	return ams-core: opts
}

let sound_opt = if let sound_fd = get_sound_fd() then
{
	"--sound-fd=" sound_fd,
}

def io_opts
{
	let modem = if let modem_fd = get_modem_fd() then
	{
		"--modem-fd=" modem_fd,
	}
	
	let opts = [ modem ]
	
	return
	if opts then
	{
		ams-io: opts,
	}
}

def auto_mbin_disk
{
	let path = root_dir "/" app_name
	
	return if exists path then {}
	else if exists path ".mbin" then
	{
		app_name ".mbin",
	}
	else if exists path ".mbim" then
	{
		app_name ".mbim",
	}
}

def fs_opts
{
	if not disks then
	{
		if let disk = getenv "AMS_DISK" or auto_mbin_disk() then
		{
			disks = [disk]
		}
	}
	
	let disk_opts = disks map
	{
		"--disk", v,
	}
	
	let appfs_opt =
	if let appfs_fd = get_appfs_fd() then
	{
		"--appfs-fd", appfs_fd,
	}
	
	let docfs_opt =
	if let docfs_fd = get_docfs_fd() then
	{
		"--docfs-fd", docfs_fd,
	}
	
	let opts = *disk_opts, appfs_opt, docfs_opt
	
	return
	if opts then
	{
		ams-fs: [ opts ],
	}
}

def seg_opts
{
	let open_opt =
	if let open = getenv "AMS_OPEN" then
	{
		assert open[ 4 ] == ':'
		
		"--type", open[ 0 -> 4], "--open", substr( open, 5 ),
	}
	
	let opts = [ open_opt ]
	
	return
	if opts then
	{
		ams-seg: opts,
	}
}

def ui_opts
{
	let debug_updates = false
	
	let opts =
	[
		"--debug-updates" (*) debug_updates,
		"--live-modal"    (*) live_modal,
	]
	
	return
	if opts then
	{
		ams-ui: opts,
	}
}

let module_args = str^
[
	core_opts(),
	fs_opts(),
	io_opts(),
	seg_opts(),
	ui_opts(),
	
	# This setting is not recommended, which is why no logic enables it.
	#vdb: [ "--continue-on-bus-error" ],
]

def argify (name)
{
	let path = module_path name
	
	return
		if name in module_args
			then {"[", path, *module_args[ name ], "]"}
			else {path}
}

let prefix_list = "core io fs snd rsrc pack seg qd ui print"
var ams_modules = [ prefix_list / ' ' ] map {"ams-" v}

ams_modules <-- "vdb"
ams_modules <-- "umsp"

let module_opts = ams_modules map {"-m", argify v}

let app = ams_bin "/app"

let works = home "/run/" getpid() ".works"

run [ "mkdir -p" / ' ', works ]

let update = works "/update-signal.fifo"
let events = works "/events.fifo"
let screen = works "/screen.skif"
let cursor = works "/cursor.skif"

let crsr_opt = "--cursor=" cursor (*) has_cursor
let ev_opt = "--events-fifo=" events
let raster = "--raster=" screen

run [ "mkfifo", update ]

run [ "skif make -fg " screen_res "*1 -c2 -m paint -R" / ' ', screen ]

if has_cursor then
{
	run [ "skif make -fg 16x16*1 -c3 -m paint" / ' ', cursor ]
}

let zoom = "-x" (getenv "AMS_ZOOM" or 1)
let event_input = events_fd_opt, ev_opt
let command-prefix = "exhibit", "-t", title, raster, event_input, zoom

let file-servers = freemountd-server, appfs-server, docfs-server
let servers = sound-server, modem-server, file-servers

let xv68k_opts = model_opt, mem_opt, sound_opt, "-tSP", crsr_opt

let xv68k-subcmd = xv68k, xv68k_opts, raster, *module_opts, app, app_name
let graft-subcmd = "graft", servers, xv68k-subcmd

let command = [ command-prefix, graft-subcmd ]

if has_cursor then
{
	warn "cursor: " cursor
}

warn "screen: " screen
warn "events: " events
warn "update: " update

setenv( "GRAPHICS_UPDATE_SIGNAL_FIFO", update )

let SIGINT = 2

signal-ignore SIGINT

run command

# We need to "unlock" the FIFO (by opening it O_RDONLY | O_NONBLOCK)
# one last time in case vnc-interact is blocked trying to open it.
# Then we delete the FIFO so it can't be opened again.

# In order to avoid a race condition, we actually rename the FIFO first.
# Unlocking the renamed FIFO unblocks vnc-interact's open() call, and if
# it manages to call open() again before we call remove(), the open() will
# fail nonetheless because the FIFO was already renamed.

let update2 = update "2"

rename( update, update2 )

try
{
	# This will throw an exception because load() doesn't work on FIFOs,
	# but it does open it O_RDONLY|O_NONBLOCK first, which is all we need.
	
	load update2
}
catch {}

remove update2
remove events
remove screen

if has_cursor then
{
	remove cursor
}

remove works

end.
