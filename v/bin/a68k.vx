#!/usr/bin/env minivx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/v/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let asm68k = import "asm68k"

let asm =      if argc < 2          then { "NOP; RTS  \n" }
          else if argv[ 1 ] == "-e" then { argv[ 2 ] "\n" }
          else                           { load argv[ 1 ] }

var p = begin asm

if let results = p += asm68k.grammar then
{
	if p.rest then
	{
		warn "leftover: " rep p.rest
	}
	
	if isatty OUT then
	{
		print " ".join results
	}
	else
	{
		OUT <== results
	}
}
else
{
	warn "no match"
}

end.
