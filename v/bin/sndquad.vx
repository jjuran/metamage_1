#!/usr/bin/env minivx

def connect_output()
{
	if let sndpipe_fd = getenv "SNDPIPE_FD" then
	{
		return fd int sndpipe_fd
	}
	
	if isatty OUT then
	{
		warn "sndquad: Pipe output to sndtrack, please"
		exit 1
	}
	
	return OUT
}

let output_fd = connect_output()

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/v/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let sndpipe = import "sndpipe"

let sound = sndpipe.player { output_fd <== _ }

let square_2 = packed (x"40" * 128, x"c0" * 128) => 0
let sawtooth = 0 -> 256 map byte per packed => 128

let triangle = do
{
	let tooth = 1 -> 128 map {byte (2 * v)} per packed
	
	packed (tooth x"FF" -tooth x"01") => 64,
}

# 1.059463094359295^12 ~= 2
# 1.059463094359295 * 2^16 ~= 69432.9733519308 ~= 69433

let semitone_numer = 69433
let semitone_denom = 2^16

let core = 0xb5c8

let adj = int if argc > 1 then {argv[ 1 ]}

def x (k)
{
	return k * core * semitone_numer^adj div semitone_denom^adj
}

let duration = 2

let chord =
[
	2: triangle,
	3: square_2,
	5: triangle,
	8: sawtooth,
]

let notes = chord map { x v.key => v.value }

sound.four_tone (duration * 60, notes)

sound.detach()

end.
