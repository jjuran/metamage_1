#!/usr/bin/env minivx

assert argv[ 1 ] == "-o"

let output_file = argv[ 2 ]
let input_files = argv[ 3 -> argc ]

var types = mb32^[]

let encode_u16BE = (big-endian u16).encode
let encode_u32BE = (big-endian u32).encode

# Dirty unhex:
# 
# Produce correct values for valid hexadecimal input,
# but also return something for invalid input.

def dirty_unhex_digit (x)
{
	let alpha = *(packed x & x"40")
	let digit = u8 x % 16
	
	return
		if alpha
			then {digit + 9}
			else {digit}
}

def dirty_unhex (x)
{
	return x map dirty_unhex_digit per 0>-{ L * 16 + v }
}

assert dirty_unhex "0000" == 0
assert dirty_unhex "0001" == 1
assert dirty_unhex "0009" == 9
assert dirty_unhex "000a" == 10
assert dirty_unhex "000f" == 15
assert dirty_unhex "ffff" == 65535

def add_rsrc (data, name)
{
	if name.length != 9 or name[ 0 ] == '.' then
	{
		return ()
	}
	
	let resid = dirty_unhex name[ 0 -> 4 ] mod 2^16
	let rtype = mb32        name[ 5 -> 9 ]
	
	if not rtype in types then
	{
		types[ rtype ] = i16^[]
	}
	
	types[ rtype ][ resid ] = data
}

for resfile in input_files do
{
	let dir = resfile "/r/"
	
	let rsrcs = listdir dir
	
	for rsrc in rsrcs do
	{
		let path = dir rsrc
		
		add_rsrc (packed load path, rsrc)
	}
}

let rsrc_count = types.values map {v.length} per Math.sum

let typelist_size = 2 + 8 * types.length

var type_list = encode_u16BE (types.length - 1)

var rsrc_headers = x""
var data_section = x""

for type_rsrcs in types do
{
	let (rtype, rsrcs) = *type_rsrcs
	
	let offset = encode_u16BE (typelist_size + rsrc_headers.size)
	
	type_list = type_list (packed rtype) encode_u16BE( rsrcs.length - 1 ) offset
	
	for id_rsrc in rsrcs do
	{
		let (id, rsrc) = *id_rsrc
		
		let offset = encode_u32BE data_section.size
		
		rsrc_headers = rsrc_headers (encode_u16BE id) x"ffff" offset x"00000000"
		data_section = data_section (encode_u32BE rsrc.size) rsrc
	}
}

let rsrc_map_size = 28 + typelist_size + rsrc_headers.size

let map_offset = 256 + data_section.size

let header_fields = [256, map_offset, data_section.size, rsrc_map_size ]

let offsets = x"001c" encode_u16BE (0x1c + typelist_size + rsrc_headers.size)

let rsrc_fork_header = header_fields map encode_u32BE per packed
let rsrc_map_header  = rsrc_fork_header (x"00" * 8) offsets

let rsrc_map = rsrc_map_header type_list rsrc_headers

var out = rewrite output_file

out <== rsrc_fork_header, x"00" * (112 + 128), data_section, rsrc_map
