#!/usr/bin/env vx -Z

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let readline = import "readline"

def error (msg) { warn msg; exit 1 }

def fail (line)  { error ("truncated response: '" line "'") }

def getline (fd) { readline( fd, fail ) }

if argc < 2 then
{
	fd 7 <== "help\r\n"
	
	for line in receiver {readline( fd 6, {} )} do
	{
		print line
	}
	
	exit 0
}

if argc < 3 then
{
	let invocation = "tcpclient <host> <port> tcpmux-client"
	
	warn "Usage:  " invocation
	warn "        " invocation " <service> <program argv>"
	
	exit 2
}

let service = argv[ 1 ]

let program-argv = argv[ 2 -> argc ]

fd 7 <== service "\r\n"

let response = getline fd 6 or error "empty response"

if response[ 0 ] != '+' then
{
	if response[ 0 ] != '-' then
	{
		error ("Invalid response: " response)
	}
	
	error response
}

execvp( program-argv[ 0 ], program-argv )

end.
