#!/usr/bin/env minivx -Z

let usr_include_portaudio_h = "/usr/include/portaudio.h"

let portaudio_repo      = "portaudio-mirror"
let portaudio_repo_path = "../" portaudio_repo

let configure_cmd = "./configure --disable-mac-universal"

let static_lib_name = "libportaudio.a"
let static_lib_subpath = "lib/.libs/" static_lib_name

def exists (path)
{
	return try {stat path; true} catch {false}
}

def installed
{
	return exists usr_include_portaudio_h
}

def get_osx_vers
{
	try
	{
		let version = output-from-run ["sw_vers -productVersion" / ' ']
		
		var p = begin version
		var q = p.find('.')
		
		let major = int (q - p)
		
		++q
		p = q.find('.' | '\n')
		
		let minor = int (p - q)
		
		return major, minor
	}
	catch
	{
	}
	
	return 0, 0
}

def check_osx_vers(major, minor)
{
	if major == 0 and not exists "/mach_kernel" then
	{
		warn "portaudio-pkg.vx: portaudio19-dev package required"
		
		exit 1
	}
	
	if major * 100 + minor < 1004 then
	{
		warn "portaudio-pkg.vx: PortAudio requires Mac OS X 10.4 minimum."
		
		exit 1
	}
}

def clone
{
	if installed() then {return ()}
	
	let (major, minor) = get_osx_vers()
	
	check_osx_vers(major, minor)
	
	if not exists portaudio_repo_path "/.git" then
	{
		run [ "bin/clone-repo", portaudio_repo ]
		
		system "cd -P " portaudio_repo_path " && git checkout origin/patches"
	}
	
	if not exists "var/links/" portaudio_repo then
	{
		system "mkdir -p var/links"
		system "cd var/links && ln -s ../../" portaudio_repo_path
		system "rm -f var/cache/compile-driver/catalog"
	}
}

def configure
{
	clone()
	
	if not installed() and not exists portaudio_repo_path "/Makefile" then
	{
		system "cd -P " portaudio_repo_path " && " configure_cmd
	}
}

def make
{
	configure()
	
	if not installed() then
	{
		system "cd -P " portaudio_repo_path " && make lib/libportaudio.la"
		
		if not exists portaudio_repo_path "/" static_lib_name then
		{
			system "cd -P " portaudio_repo_path " && ln -s " static_lib_subpath
		}
	}
}

let commands = str^[ :clone, :configure, :make ]

if argc < 2 then
{
	exit 0
}

let subcommand = argv[ 1 ]

if not subcommand in commands then
{
	throw subcommand ": no such portaudio-pkg command"
}

let handler = commands[ subcommand ]

handler()

end.
