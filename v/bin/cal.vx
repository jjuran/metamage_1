#!/usr/bin/env vx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../lib/v/"

def import (lib) { eval (load, realpath) libdir lib ".vobj" }

let display-grid = import "display-grid"

let wdays = ["Sun Mon Tue Wed Thu Fri Sat" / ' ']

let offsets =
[
	1, 4, 4,
	0, 2, 5,
	0, 3, 6,
	1, 4, 6,
]

let mdays =
[
	31, 28, 31,
	30, 31, 30,
	31, 31, 30,
	31, 30, 31,
]

def is_leap_year (y)
{
	return +(y % 4 == 0) - +(y % 100 == 0) + +(y % 400 == 0)
}

def days_in_month (month, leap_year)
{
	return mdays[ month - 1 ] + leap_year * +(month == 2)
}

def weekday_of (year, month, day)
{
	let century = year div 100
	let ydigits = year mod 100
	
	let leap_year = is_leap_year year
	
	let leap = leap_year * +(month <= 2)
	
	let offset = offsets[ month - 1 ]
	
	let fudge = 19 - century  # works for 19xx and 20xx
	
	let x = ydigits + ydigits div 4 + offset + day - 1 - leap + fudge
	
	return x mod 7
}

def make_cal (offset, n_days)  # offset = 0 for Sunday, 1 for Monday, etc.
{
	let one =  1 ..      9 map {"  " str _}
	let two = 10 .. n_days map {" "  str _}
	
	let extra = 6 * 7 - (offset + n_days)
	
	let cells = "" (*) offset, *one, *two, "" (*) extra
	
	return [7, 7]: [ *wdays, cells ]
}

def calendar_month (year, month, day)
{
	let offset = weekday_of( year, month, 1 )
	
	let days = days_in_month( month, is_leap_year year )
	
	var cal = make_cal( offset, days )
	
	if day then
	{
		let i = 7 + offset + day - 1
		
		var cells = cal.value
		
		cells[ i ][ 0 ] = '>'
		
		cal = cal.key => cells
	}
	
	return cal
}

def ymd
{
	if argv.length == 1 then
	{
		let date-time = import "../lib/v/date-time"
		
		return *[ date-time.time_pieces time() ][ 0 .. 2 ]
	}
	
	if argv.length < 3 then
	{
		warn "Usage:  cal <year> <month>"
		warn "        cal <year> <month> <day>"
		exit 2
	}
	
	if argv.length == 3 then
	{
		return *(argv[ 1 .. 2 ] map int), 0
	}
	
	return *(argv[ 1 .. 3 ] map int)
}

let year, let month, let day = ymd()

print display-grid calendar_month( year, month, day )

end.
